# null resource to handle replication and object removal when destroying
resource "null_resource" "remove_replication" {
  triggers = {
    bucket_id = aws_s3_bucket.terraform_state.id
  }

  provisioner "local-exec" {
    when    = destroy
    command = <<EOF
    aws s3api delete-bucket-replication --bucket ${self.triggers.bucket_id}
    aws s3 rm s3://${self.triggers.bucket_id} --recursive
    EOF
  }
}

# ------------------------------------------------------------------------------------
# Create a new S3 bucket for Terraform state
# ------------------------------------------------------------------------------------

# S3 bucket for Terraform state
resource "aws_s3_bucket" "terraform_state" {
  bucket        = "${local.bucket_name}"
  force_destroy = true

  # Prevent/Allow accidental deletion
  lifecycle {
    prevent_destroy = false
  }

  tags = local.common_tags

  #checkov:skip=CKV2_AWS_61: "Ensure that an S3 bucket has a lifecycle configuration"
  #checkov:skip=CKV_AWS_145: "Ensure that S3 buckets are encrypted with KMS by default"
}

#  Enable Access Logging
resource "aws_s3_bucket_logging" "terraform_state" {
  bucket        = aws_s3_bucket.terraform_state.id
  target_bucket = aws_s3_bucket.terraform_state.id
  target_prefix = "logs/"
}

#Enable event notification
resource "aws_s3_bucket_notification" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id

  eventbridge = true
}

# Enable versioning
resource "aws_s3_bucket_versioning" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id
  versioning_configuration {
    status = "Enabled"
  }
}

# Enable encryption
resource "aws_s3_bucket_server_side_encryption_configuration" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

# Block public access
resource "aws_s3_bucket_public_access_block" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

# Add lifecycle rules
resource "aws_s3_bucket_lifecycle_configuration" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id

  rule {
    id     = "state-backup"
    status = "Enabled"
    
    filter {
      prefix = ""
    }

    transition {
      days          = 30
      storage_class = "STANDARD_IA"
    }

    transition {
      days          = 90
      storage_class = "GLACIER"
    }

    noncurrent_version_transition {
      noncurrent_days = 7
      storage_class   = "GLACIER"
    }

    expiration {
      expired_object_delete_marker = true
    }

    abort_incomplete_multipart_upload {
      days_after_initiation = 7
    }
  }
}

# Enable replication
resource "aws_s3_bucket_replication_configuration" "terraform_state" {
  bucket = aws_s3_bucket.terraform_state.id
  role   = aws_iam_role.replication.arn

  rule {
    id     = "state-replication"
    status = "Enabled"

    destination {
      bucket        = aws_s3_bucket.terraform_state_replica.arn
      storage_class = "STANDARD_IA"
    }
  }

  lifecycle {
    create_before_destroy = false
  }
}

resource "aws_kms_key" "terraform_state" {
  description         = "KMS key for Terraform state"
  enable_key_rotation = true
  is_enabled          = true
  policy = templatefile("${path.module}/../infra/policies/kms-policy.json", {
    account_id = data.aws_caller_identity.current.account_id
  })

  tags = local.common_tags
}

resource "aws_kms_alias" "terraform_state_alias" {
  name          = "alias/${local.name_prefix}-terraform-state"
  target_key_id = aws_kms_key.terraform_state.key_id
}

#----------------------------------------------------------------------------------
# Replication bucket
#----------------------------------------------------------------------------------

# Create replication bucket in secondary region
resource "aws_s3_bucket" "terraform_state_replica" {
  provider = aws.secondary
  bucket   = "${local.replica_bucket_name}"
  force_destroy = true

  lifecycle {
    prevent_destroy = false
  }

  tags = local.common_tags

  #checkov:skip=CKV2_AWS_61: "Ensure that an S3 bucket has a lifecycle configuration"
  #checkov:skip=CKV_AWS_145: "Ensure that S3 buckets are encrypted with KMS by default"
  #checkov:skip=CKV_AWS_18: "Ensure the S3 bucket has access logging enabled"
  #checkov:skip=CKV2_AWS_62: "Ensure S3 buckets should have event notifications enabled"
}

# Enable versioning on replica bucket
resource "aws_s3_bucket_versioning" "terraform_state_replica" {
  provider = aws.secondary
  bucket   = aws_s3_bucket.terraform_state_replica.id
  versioning_configuration {
    status = "Enabled"
  }
}

# Ensure public access is blocked
resource "aws_s3_bucket_public_access_block" "terraform_state_replica" {
  provider = aws.secondary
  bucket   = aws_s3_bucket.terraform_state_replica.id
  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

# Enable access logging
resource "aws_s3_bucket_logging" "terraform_state_replica" {
  provider = aws.secondary
  bucket   = aws_s3_bucket.terraform_state_replica.id
  target_bucket = aws_s3_bucket.terraform_state_replica.id
  target_prefix = "logs/"
}